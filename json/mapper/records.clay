import json.support.records.*;
import json.support.values.*;

import json.model.*;
import json.mapper.*;



// overload to specify custom json field name for record field
// executed at compile time, must return static
[R, N]
RecordJsonFieldName(static R, static N) = N;

// overload to specify custon json field name for record field
// execute at mapper construction time, so mapToJsonQuick may be slow
// necessary when field name cannot be computed at compile time
[R, N]
define recordJsonFieldName(static R, static N);

// overload to exclude some field from serialization and deserialization
[R, N]
IncludeRecordJsonField(static R, static N) = true;



// field json name holder

JsonNameHolder?(T) = false;
[R | JsonNameHolder?(R)]
define jsonNameHolderGet(r: R);

[R, FieldName]
define JsonNameHolder(static R, static FieldName);


// static name holder

record JsonNameHolderStatic[JsonFieldName] ();

[JsonFieldName]
overload JsonNameHolder?(static JsonNameHolderStatic[JsonFieldName]) = true;

[R, FieldName]
overload JsonNameHolder(static R, static FieldName) =
    JsonNameHolderStatic[RecordJsonFieldName(R, FieldName)];

[JsonFieldName]
overload jsonNameHolderGet(r: JsonNameHolderStatic[JsonFieldName]) = StaticName(JsonFieldName);

// dynamic name holder

[R, FieldName]
record JsonNameHolderDynamic[R, FieldName] (
    jsonFieldName: String,
);

[R, FieldName]
overload JsonNameHolderDynamic[R, FieldName]() =
    JsonNameHolderDynamic[R, FieldName](recordJsonFieldName(R, FieldName));

[R, FieldName]
overload JsonNameHolder?(static JsonNameHolderDynamic[R, FieldName]) = true;

[R, FieldName | CallDefined?(recordJsonFieldName, Static[R], Static[FieldName])]
overload JsonNameHolder(static R, static FieldName) =
    JsonNameHolderDynamic[R, FieldName];

[R, FieldName]
overload jsonNameHolderGet(r: JsonNameHolderDynamic[R, FieldName]) = r.jsonFieldName;


// 

[R, FieldName]
record RecordFieldJsonMapperDefault[R, FieldName] (
    jsonNameHolder: JsonNameHolder(static R, static FieldName),
    valueMapper: JsonMapper(RecordFieldTypeByName(R, FieldName)),
);

[R, FieldName]
RecordFieldJsonMapperFieldName(static RecordFieldJsonMapperDefault[R, FieldName]) = FieldName;


[R, FieldName]
RecordFieldJsonMapper(static R, static FieldName) = RecordFieldJsonMapperDefault[R, FieldName];


[R, FieldName]
mapRecordFieldToJson(m: RecordFieldJsonMapperDefault[R, FieldName], r: R, j) {
    alias FieldName = RecordFieldJsonMapperFieldName(Type(m));
    var jsonName = jsonNameHolderGet(m.jsonNameHolder);
    var v = JsonValue(mapToJson(m.valueMapper, fieldRef(r, FieldName)));
    put(j, jsonName, v);
}

[R, FieldName]
mapRecordFieldFromJson(m: RecordFieldJsonMapperDefault[R, FieldName], j, r: R) {
    alias FieldName = RecordFieldJsonMapperFieldName(Type(m));
    var jsonName = jsonNameHolderGet(m.jsonNameHolder);
    var jsonFieldValue = index(j, jsonName);
    fieldRef(r, FieldName) = mapFromJson(m.valueMapper, jsonFieldValue);
}



[R]
MakeRecordJsonMapper(static R) {
    var f = name -> [static name, RecordFieldJsonMapper(R, name)];
    return ..mapValues(f, ..filterValues(Name -> IncludeRecordJsonField(R, Name), ..RecordFieldNames(R)));
}

record RecordJsonMapper[R] = ..MakeRecordJsonMapper(R);

[R]
overload JsonMapperMaps(static RecordJsonMapper[R]) = R, JsonObject;


[R | Record?(R)]
overload JsonMapper(static R) = RecordJsonMapper[R];

[R]
overload mapToJson(m: RecordJsonMapper[R], r: R) {
    var j = JsonObject();
    ..for (fieldMapper in ..recordFields(m)) {
        mapRecordFieldToJson(fieldMapper, r, j);
    }
    return j;
}

[R]
overload mapFromJson(m: RecordJsonMapper[R], j: JsonObject) {
    var r = R();
    ..for (fieldMapper in ..recordFields(m)) {
        mapRecordFieldFromJson(fieldMapper, j, r);
    }
    return r;
}



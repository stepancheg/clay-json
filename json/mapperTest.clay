import test.*;

import json.*;
import json.mapper.*;

record Album (
    title: String,
    year: UInt,
);

testRecordSimpleToJson(test) {
    var a = Album(String("Jamaicanization"), 2011u);
    var j = mapToJsonQuick(a);
    expectEqual(test, "record", JsonObject([title: "Jamaicanization", year: 2011]), j);
}

testRecordSimpleFromJson(test) {
    var j = JsonObject([title: "Wildhoney", year: 1994]);
    var expected = Album(String("Wildhoney"), 1994u);
    expectEqual(test, "record", expected, mapFromJsonQuick(Album, j));
    expectEqual(test, "record", expected, mapFromJsonQuick(Album, JsonValue(j)));
}

record Result (
    successful: Bool,
    returnValue: String,
);

overload RecordJsonFieldName(static Result, static #"returnValue") = #"return";

testRecordWithCustomStaticFieldNameToJson(test) {
    var r = Result(true, String("yep"));
    var j = mapToJsonQuick(r);
    expectEqual(test, "record", JsonObject([["successful", true], ["return", "yep"]]), j);
}

testRecordWithCustomStaticFieldNameFromJson(test) {
    var j = JsonObject([["successful", false], ["return", "no"]]);
    var expected = Result(false, String("no"));
    expectEqual(test, "record", expected, mapFromJsonQuick(Result, j));
    expectEqual(test, "record", expected, mapFromJsonQuick(Result, JsonValue(j)));
}


record Building (
    height: Int,
);

overload recordJsonFieldName(static Building, static #"height") = str("height", size("height"));

testRecordWithCustomDynamicFieldNameToJson(test) {
    var r = Building(100);
    var j = mapToJsonQuick(r);
    expectEqual(test, "record", JsonObject([height6: 100]), j);
}

testRecordWithCustomDynamicFieldNameFromJson(test) {
    var j = JsonObject([height6: 22]);
    var expected = Building(22);
    expectEqual(test, "record", expected, mapFromJsonQuick(Building, j));
}


record RecordWithTransientField (
    color: String,
    colorNumberCache: Int,
);

overload IncludeRecordJsonField(static RecordWithTransientField, static #"colorNumberCache") = false;

testRecordWithExcludedFieldToJson(test) {
    var r = RecordWithTransientField(String("red"), 0xff0000);
    var j = mapToJsonQuick(r);
    expectEqual(test, "record", JsonObject([color: "red"]), j);
}

testRecordWithExcludedFieldFromJson(test) {
    var j = JsonObject([color: "blue"]);
    var expected = RecordWithTransientField(String("blue"), 0);
    expectEqual(test, "record", expected, mapFromJsonQuick(RecordWithTransientField, j));
}


mapperTestSuite() =
    TestSuite(
        "mapper", array(
            TestCase("string to json", test => {
                var j = mapToJsonQuick(String("bla"));
                expectEqual(test, "string", JsonString("bla"), j);
            }),
            TestCase("string from json", test => {
                var j = JsonString("The angels have the phone box");
                expectEqual(test, "string", "The angels have the phone box", mapFromJsonQuick(String, j));
                expectEqual(test, "string", "The angels have the phone box", mapFromJsonQuick(String, JsonValue(j)));
            }),
            TestCase("numbers to json", test => {
                // TODO: float, decimal
                ..for (i in 1, 2u, 3ul, 4s) {
                    expectEqual(test, "number", JsonNumber(i), mapToJsonQuick(i));
                }
            }),
            TestCase("numbers from json", test => {
                // TODO: float, decimal
                ..for (i in 1, 2u, 3ul, 4s) {
                    expectEqual(test, "number", i, mapFromJsonQuick(Type(i), JsonNumber(i)));
                    expectEqual(test, "number", i, mapFromJsonQuick(Type(i), JsonValue(JsonNumber(i))));
                }
            }),
            TestCase("bool to json", test => {
                var j = mapToJsonQuick(true);
                expectEqual(test, "boolean", JsonBoolean(true), j);
            }),
            TestCase("bool from json", test => {
                var j = JsonBoolean(true);
                expectEqual(test, "boolean", true, mapFromJsonQuick(Bool, j));
                expectEqual(test, "boolean", true, mapFromJsonQuick(Bool, JsonValue(j)));
            }),
            TestCase("Vector[Int] to json", test => {
                var v = Vector[Int](1, 4, 8, 16);
                var expected = JsonArray([1, 4, 8, 16]);
                expectEqual(test, "array", expected, mapToJsonQuick(v));
            }),
            TestCase("Vector[Int] from json", test => {
                var j = JsonArray([2, 3, 4]);
                var expected = Vector[Int](2, 3, 4);
                expectEqual(test, "array", expected, mapFromJsonQuick(Vector[Int], j));
            }),
            TestCase("record simple to json", testRecordSimpleToJson),
            TestCase("record simple from json", testRecordSimpleFromJson),
            TestCase("record with custom static field name to json", testRecordWithCustomStaticFieldNameToJson),
            TestCase("record with custom static field name from json", testRecordWithCustomStaticFieldNameFromJson),
            TestCase("record with custom dynamic field name to json", testRecordWithCustomDynamicFieldNameToJson),
            TestCase("record with custom dynamic field name from json", testRecordWithCustomDynamicFieldNameFromJson),
            TestCase("record with excluded field to json", testRecordWithExcludedFieldToJson),
            TestCase("record with excluded field from json", testRecordWithExcludedFieldFromJson),
        )
    );

mapperTestMain() = testMain(mapperTestSuite());

main() = mapperTestMain();



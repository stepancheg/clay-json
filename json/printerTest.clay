import printer.*;

import test.*;

import json.*;
import json.printer.*;

[V when VariantMember?(JsonValue, V)]
private expectPrinted(expected, value: V) {
    expectEqual(expected, str(value));
    expectEqual(expected, str(JsonValue(value)));
}

[V when VariantMember?(JsonValue, V)]
private expectPrintedPretty(expected, value: V) {
    expectEqual(expected, strPretty(value));
    expectEqual(expected, strPretty(JsonValue(value)));
}

printerTestSuite() =
    TestSuite(
        "printer", array(
            TestCase("null", => {
                expectPrinted("null", JsonNull());
            }),
            TestCase("boolean", => {
                expectPrinted("true", JsonBoolean(true));
                expectPrinted("false", JsonBoolean(false));
            }),
            TestCase("number", => {
                expectPrinted("12", JsonNumber(12));
            }),
            TestCase("string", => {
                expectPrinted("\"abc\"", JsonString("abc"));
            }),
            TestCase("array", => {
                var array = JsonArray();
                expectPrinted("[]", array);
                push(array, 17);
                expectPrinted("[17]", array);
                push(array, "xy");
                expectPrinted("[17, \"xy\"]", array);
            }),
            TestCase("array pretty", => {
                var array = JsonArray();
                expectPrintedPretty("[]", array);
                push(array, 37);
                expectPrintedPretty("[\n    37\n]", array);
                push(array, "zw");
                expectPrintedPretty("[\n    37,\n    \"zw\"\n]", array);
            }),
            TestCase("object", => {
                var object = JsonObject();
                expectPrinted("{}", object);
                put(object, "hello", 5);
                expectPrinted("{\"hello\": 5}", object);
                put(object, "world", JsonArray());
                expectPrinted("{\"hello\": 5, \"world\": []}", object);
            }),
            TestCase("object pretty", => {
                var object = JsonObject();
                expectPrintedPretty("{}", object);
                put(object, "hello", 9);
                expectPrintedPretty("{\n    \"hello\": 9\n}", object);
                put(object, "world", JsonArray());
                expectPrintedPretty("{\n    \"hello\": 9,\n    \"world\": []\n}", object);
            }),
        )
    );

printerTestMain() = testMain(printerTestSuite());

main() = printerTestMain();


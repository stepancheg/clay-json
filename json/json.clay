import vectors.*;
import maybe.*;
import numbers.parser.*;

import json.escape.*;
import json.printer.*;
import json.support.decimal.*;

/// model

record JsonArray (
    data: Vector[JsonValue]
);

alias JsonObjectKeyValue = Tuple[String, JsonValue];

record JsonObject (
    data: Vector[JsonObjectKeyValue]
);

record JsonNull (
);

record JsonNumber (
    value: Decimal
);

record JsonString (
    // unquoted
    value: String
);

record JsonBoolean (
    value: Bool
);

variant JsonValue (JsonArray, JsonObject, JsonNull, JsonNumber, JsonString, JsonBoolean);

[J]
JsonVariantMember?(static J) = VariantMember?(JsonValue, J);
[J]
JsonAnything?(static J) = J == JsonValue or JsonVariantMember?(J);

isArray(value) = variantIs?(value, JsonArray);
isObject(value) = variantIs?(value, JsonObject);
isNull(value) = variantIs?(value, JsonNull);
isNumber(value) = variantIs?(value, JsonNumber);
isString(value) = variantIs?(value, JsonString);
isBoolean(value) = variantIs?(value, JsonBoolean);

getArray(forward value) = forward variantAs(value, JsonArray);
getObject(forward value) = forward variantAs(value, JsonObject);
getNull(forward value) = forward variantAs(value, JsonNull);
getNumber(forward value) = forward variantAs(value, JsonNumber);
getString(forward value) = forward variantAs(value, JsonString);
getBoolean(forward value) = forward variantAs(value, JsonBoolean);

overload Bool(value: JsonValue) = getBoolean(value).value;
overload Decimal(value: JsonValue) = getNumber(value).value;

[I | Numeric?(I)]
overload I(value: JsonValue) = I(Decimal(value));

overload String(value: JsonValue) = getString(value).value;

[N | N != Decimal and N != JsonNumber]
overload JsonNumber(n: N) = JsonNumber(Decimal(n));

[N | Numeric?(N)]
overload N(j: JsonNumber) = N(j.value);

[N | Numeric?(N)]
overload N(j: JsonValue) = N(getNumber(j));

[S | S != String and S != JsonString]
overload JsonString(s: S) = JsonString(String(s));

[..T]
overload JsonArray(v: Tuple[..T]) {
    var r = JsonArray();
    push(r, ..unpackTuple(v));
    return r;
}

[S | Sequence?(S) and S != JsonArray]
overload JsonArray(s: S) {
    var r = JsonArray();
    for (item in s) {
        push(r, item);
    }
    return r;
}

private define stringForKey(s): String;
[S | String?(S)]
overload stringForKey(s: S) = String(s);
[S]
overload stringForKey(static S) = stringForKey(StaticName(S));

[T]
private KeyValueTuple?(static T) = Tuple?(T) and TupleElementCount(T) == 2 and CallDefined?(stringForKey, nthValue(static 0, ..TupleElements(T)));

[..T | allValues?(KeyValueTuple?, ..T)]
overload JsonObject(kvs: Tuple[..T]) {
    var r = JsonObject();
    ..for (kv in ..unpackTuple(kvs)) {
        put(r, kv.0, kv.1);
    }
    return r;
}


[S | String?(S)]
overload JsonValue(s: S) = JsonValue(JsonString(s));
[N | Numeric?(N)]
overload JsonValue(n: N) = JsonValue(JsonNumber(n));
[]
overload JsonValue(b: Bool) = JsonValue(JsonBoolean(b));
[]
overload JsonValue() = JsonValue(JsonNull());

[A | CallDefined?(JsonValue, A) and A != JsonValue]
overload assign(ref v: JsonValue, forward a: A) {
    v = JsonValue(a);
}

[..V | countValues(..V) > 0]
overload push(array: JsonArray, ..value: V) =
    ..push(array.data, ..mapValues(JsonValue, ..value));

overload push(object: JsonObject, value) =
    ..push(object.data, value);

put(object: JsonObject, key, value) =
    ..push(object.data, [stringForKey(key), JsonValue(value)]);

overload index(array: JsonArray, i) =
    ref index(array.data, i);

overload index(object: JsonObject, k) {
    var adjustedK = stringForKey(k);
    for (t in object.data) {
        if (t.0 == adjustedK)
            return ref t.1;
    }
    error("no key ", k);
    throw Error();
}

[I | Integer?(I)]
overload index(array: JsonValue, i: I) =
    forward index(getArray(array), i);

[K | String?(K)]
overload index(object: JsonValue, k: K) =
    forward index(getObject(object), k);

[K]
overload fieldRef(object: JsonObject, static K) =
    forward index(object, StaticName(K));

overload fieldRef(object: JsonObject, static #"data") =
    ref recordFieldRefByName(object, #"data");

[K]
overload fieldRef(value: JsonValue, static K) =
    fieldRef(getObject(value), static K);

[J | JsonVariantMember?(J)]
overload size(j: J): SizeT {
    error(J, " has no size");
    throw Error();
}
overload size(array: JsonArray) = size(array.data);
overload size(object: JsonObject) = size(object.data);
overload size(string: JsonString) = size(string.value);

overload size(value: JsonValue) = size(*value);

overload iterator(array: JsonArray) = iterator(array.data);
overload iterator(object: JsonObject) = iterator(object.data);


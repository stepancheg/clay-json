import printer.*;

import test.*;

import json.support.decimal.*;

decimalTestSuite() =
    TestSuite(
        "decimal", array(
            TestCase("str", => {
                expectEqual("3e5", str(Decimal(3, 5)));
                expectEqual("1213", str(Decimal(1213, 0)));
                expectEqual("121.3", str(Decimal(1213, -1)));
                expectEqual("12.13", str(Decimal(1213, -2)));
                expectEqual("1.213", str(Decimal(1213, -3)));
                expectEqual("1213e-4", str(Decimal(1213, -4)));
                expectEqual("1213e-5", str(Decimal(1213, -5)));
                expectEqual("1213e-6", str(Decimal(1213, -6)));
            }),
            TestCase("parse", => {
                expectEqual(Decimal(3, 5), Decimal("3e5"));
                try {
                    Decimal("3e5x");
                    expectTrue(false);
                } catch (e) {
                    // ok
                }
            }),
            TestCase("==", => {
                expectEqual(Decimal(1000, 0), Decimal(1, 3));
                expectEqual(Decimal(1000, -10), Decimal(1, -7));
                expectNotEqual(Decimal(1003, 0), Decimal(1, 3));
                expectNotEqual(Decimal(1000, 0), Decimal(1004, 0));
            }),
            TestCase("== negative", => {
                expectEqual(Decimal(-1000, 0), Decimal(-1, 3));
                expectNotEqual(Decimal(-1000, 30), Decimal(1000, 30));
            }),
            TestCase("without scale", => {
                expectEqual(Decimal(1200), Decimal(12, 2));
            }),
            TestCase("convert to integer", => {
                expectEqual(Int(Decimal(1000, -3)), 1);
                expectEqual(Int(Decimal(1000, 3)), 1000000);
                expectEqual(Int(Decimal(-1000, -3)), -1);
            }),
            TestCase("convert to float", => {
                expectEqual(1.5f, Float(Decimal(15, -1)));
                expectEqual(1.5e5fl, Float64(Decimal(15, 4)));
            }),
            TestCase("changeScale", => {
                var d = Decimal(1234, 4);
                var d1 = changeScale(d, 1);
                expectEqual(d, d1);
            }),
            TestCase("shiftScale", => {
                var d = Decimal(1234, 4);
                var d1 = shiftScale(d, -2);
                expectEqual(d, d1);
            }),
            TestCase("exp10", => {
                var d = exp10(Decimal(123, 2), -3);
                var expected = Decimal(123, -1);
                expectEqual(expected, d);
            }),
            TestCase("+", => {
                expectEqual(Decimal(1012), Decimal(1, 3) + Decimal(12));
            }),
            TestCase("*", => {
                expectEqual(Decimal(43520), Decimal(128, 2) * Decimal(34, -1));
            }),
            TestCase("unary -", => {
                expectEqual(Decimal(-1200), -Decimal(12, 2));
            }),
        )
    );

decimalTestMain() = testMain(decimalTestSuite());

main() = decimalTestMain();


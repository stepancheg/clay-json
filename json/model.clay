import data.strings.*;
import data.vectors.*;
import printer.*;
import data.strings.encodings.utf8.*;

import json.support.decimal.*;
import json.support.unicode.*;

/// model

record JsonArray (
    data: Vector[JsonValue]
);

alias JsonObjectKeyValue = Tuple[UTF8String, JsonValue];

record JsonObject (
    data: Vector[JsonObjectKeyValue]
);

record JsonNull (
);

record JsonNumber (
    value: Decimal
);

record JsonString (
    // unquoted
    value: UTF8String
);

record JsonBoolean (
    value: Bool
);

variant JsonValue (JsonArray, JsonObject, JsonNull, JsonNumber, JsonString, JsonBoolean);

// https://github.com/jckarter/clay/issues/332#issuecomment-7827948
//overload CompareOpDefined?((==), #JsonValue, #JsonValue) = true;

[J]
JsonVariantMember?(#J) = VariantMember?(JsonValue, J);
[J]
JsonAnything?(#J) = J == JsonValue or JsonVariantMember?(J);

isArray(value) = variantIs?(value, JsonArray);
isObject(value) = variantIs?(value, JsonObject);
isNull(value) = variantIs?(value, JsonNull);
isNumber(value) = variantIs?(value, JsonNumber);
isString(value) = variantIs?(value, JsonString);
isBoolean(value) = variantIs?(value, JsonBoolean);

getArray(forward value) = forward variantAs(value, JsonArray);
getObject(forward value) = forward variantAs(value, JsonObject);
getNull(forward value) = forward variantAs(value, JsonNull);
getNumber(forward value) = forward variantAs(value, JsonNumber);
getString(forward value) = forward variantAs(value, JsonString);
getBoolean(forward value) = forward variantAs(value, JsonBoolean);

overload Bool(value: JsonValue) = getBoolean(value).value;
overload Decimal(value: JsonValue) = getNumber(value).value;

[I when Numeric?(I)]
overload I(value: JsonValue) = I(Decimal(value));

overload UTF8String(value: JsonValue) = getString(value).value;
overload String(value: JsonValue) = UTF8String(value).encoded;

[N when N != Decimal and N != JsonNumber]
overload JsonNumber(n: N) = JsonNumber(Decimal(n));

[N when Numeric?(N)]
overload N(j: JsonNumber) = N(j.value);

[N when Numeric?(N)]
overload N(j: JsonValue) = N(getNumber(j));

[S when S != UTF8String and S != JsonString]
overload JsonString(s: S) = JsonString(UTF8String(s));

[..T]
overload JsonArray(v: Tuple[..T]) {
    var r = JsonArray();
    push(r, ..unpackTuple(v));
    return r;
}

[S when Sequence?(S) and S != JsonArray]
overload JsonArray(s: S) {
    var r = JsonArray();
    for (item in s) {
        push(r, item);
    }
    return r;
}

private define stringForKey(s): UTF8String;
[S when String?(S)]
overload stringForKey(s: S) = UTF8String(s);
[S]
overload stringForKey(#S) = stringForKey(CStringRef(StaticName(S)));

[T]
private KeyValueTuple?(#T) = Tuple?(T) and TupleElementCount(T) == 2 and CallDefined?(stringForKey, nthValue(#0, ..TupleElements(T)));

[..T when allValues?(KeyValueTuple?, ..T)]
overload JsonObject(kvs: Tuple[..T]) {
    var r = JsonObject();
    ..for (kv in ..unpackTuple(kvs)) {
        put(r, kv.0, kv.1);
    }
    return r;
}


[S when String?(S)]
overload JsonValue(s: S) = JsonValue(JsonString(s));
[N when Numeric?(N)]
overload JsonValue(n: N) = JsonValue(JsonNumber(n));
[]
overload JsonValue(b: Bool) = JsonValue(JsonBoolean(b));
[]
overload JsonValue() = JsonValue(JsonNull());

[A when CallDefined?(JsonValue, A) and A != JsonValue]
overload assign(ref v: JsonValue, forward a: A) {
    v = JsonValue(a);
}

[..V when countValues(..V) > 0]
overload push(array: JsonArray, ..value: V) =
    ..push(array.data, ..mapValues(JsonValue, ..value));

overload push(object: JsonObject, value) =
    ..push(object.data, value);

put(object: JsonObject, key, value) =
    ..push(object.data, [stringForKey(key), JsonValue(value)]);

overload index(array: JsonArray, i) =
    ref index(array.data, i);

overload index(object: JsonObject, k) {
    var adjustedK = stringForKey(k);
    for (t in object.data) {
        if (t.0 == adjustedK)
            return ref t.1;
    }
    error("no key ", k);
    throw Error(CStringRef());
}

[I when Integer?(I)]
overload index(array: JsonValue, i: I) =
    forward index(getArray(array), i);

[K when String?(K)]
overload index(object: JsonValue, k: K) =
    forward index(getObject(object), k);

[K]
overload fieldRef(object: JsonObject, #K) =
    forward index(object, StaticName(K));

overload fieldRef(object: JsonObject, #"data") =
    ref recordFieldRefByName(object, #"data");

[K]
overload fieldRef(value: JsonValue, #K) =
    fieldRef(getObject(value), #K);

[J when JsonVariantMember?(J)]
overload size(j: J): SizeT {
    error(J, " has no size");
    throw Error(CStringRef());
}
overload size(array: JsonArray) = size(array.data);
overload size(object: JsonObject) = size(object.data);

overload size(value: JsonValue) = size(*value);

overload iterator(array: JsonArray) = iterator(array.data);
overload iterator(object: JsonObject) = iterator(object.data);


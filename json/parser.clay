import parsing.combinators.strings.*;
import parsing.combinators.wrapper.*;
import vectors.*;
import strings.*;
import printer.*;

import json.*;
import json.escape.*;
import json.support.decimal.*;
import json.support.unicode.*;


// tokenizer

private enum JsonTokenType (
    OPENING_BRACE_TYPE,
    CLOSING_BRACE_TYPE,
    OPENING_BRACKET_TYPE,
    CLOSING_BRACKET_TYPE,
    COMMA_TYPE,
    COLON_TYPE,
    NUMBER_TYPE,
    STRING_TYPE,
    TRUE_TYPE,
    FALSE_TYPE,
    NULL_TYPE,
);

private variant JsonTokenData (UTF8String, Decimal, Nothing);

private record JsonToken (
    type: JsonTokenType,
    data: JsonTokenData,
);

private keywordTokenParser(tokenType, keyword) = modify(t => JsonToken(tokenType, JsonTokenData(Nothing)), literalString(str(keyword)));

private var digit19Parser = condition(c => ((c >= '1') and (c <= '9')));
private var digitParser = condition(c => ((c >= '0') and (c <= '9')));

private var hexdigitValueParser =
    choice(
        modify(c => UInt(c) - UInt('0'),      condition(c => (c >= '0') and (c <= '9'))),
        modify(c => UInt(c) - UInt('a') + 10, condition(c => (c >= 'a') and (c <= 'f'))),
        modify(c => UInt(c) - UInt('A') + 10, condition(c => (c >= 'A') and (c <= 'F'))),
    );


private digitValue(digit) = UInt(digit) - UInt('0');

private foldBeforeDot(t) {
    var firstDigit = t.0;
    var rest = t.1;
    var r = UInt64(digitValue(firstDigit));
    for (c in rest) {
        r = r * 10 + digitValue(c);
    }
    return Decimal(r);
}

private foldAfterDot(digits) {
    var r = 0ul;
    for (c in digits) {
        r = r * 10 + digitValue(c);
    }
    return Decimal(r, -Int(size(digits)));
}

private foldUnsignedIntegerDigits(digits) {
    var r = 0ul;
    for (c in digits) {
        r = r * 10 + digitValue(c);
    }
    return r;
}

var unsignedIntegerParser =
    modify(foldUnsignedIntegerDigits, oneOrMore(digitParser));

var signedIntegerParser =
    modify(t -> t.0 * Int64(t.1), [
        modify(o -> maybe(o, some -> some, () -> 1), optional(
            modify(c -> if (c == '+') 1 else -1, choice('+', '-'))
        )),
        unsignedIntegerParser,
    ]);

private var decimalParser =
    modify(
        t => {
            var minus? = just?(t.0);
            var digitsBeforeDot = t.1;
            var digitsAfterDot = t.2;
            var exp = t.3;
            
            var r = exp10(digitsBeforeDot + digitsAfterDot, exp);
            if (minus?)
                r = -r;
            
            return r;
        },
        [
            optional('-'),
            choice(
                modify(x -> Decimal(0), '0'),
                modify(foldBeforeDot, [digit19Parser, zeroOrMore(digitParser)]),
            ),
            modify(o -> maybe(o, t -> t.1, () -> Decimal(0)), optional([
                '.',
                modify(foldAfterDot, oneOrMore(digitParser)),
            ])),
            modify(o -> maybe(o, t -> t.1, () -> 0l), optional([
                choice('e', 'E'),
                signedIntegerParser,
            ])),
        ]
    );

unescapeCharParser(escape, value) =
    modify(c => UniChar(value), condition(c => c == escape));

private makeU(d1, d2, d3, d4): UniChar =
    UniChar(bitor(bitshl(d1, 12), bitshl(d2, 8), bitshl(d3, 4), bitshl(d4, 0)));

private var stringCharParser =
    choice(
        modify(t -> t.1,
            [
                condition(c -> c == '\\'),
                choice(
                    modify(UniChar, condition(c -> c == '"' or c == '\\')),
                    unescapeCharParser('n', '\n'),
                    unescapeCharParser('r', '\r'),
                    unescapeCharParser('t', '\t'),
                    unescapeCharParser('f', '\x0c'),
                    unescapeCharParser('b', '\x08'),
                    modify(t -> makeU(t.1, t.2, t.3, t.4), [
                        condition(c -> c == 'u'),
                        hexdigitValueParser,
                        hexdigitValueParser,
                        hexdigitValueParser,
                        hexdigitValueParser,
                    ]),
                ),
            ]
        ),
        modify(UniChar, condition(c -> c != '"')),
    );

private var stringTokenParser =
    modify(
        t -> JsonToken(STRING_TYPE, JsonTokenData(UTF8String(t.1))),
        [
            '"',
            zeroOrMore(stringCharParser),
            '"',
        ]
    );


private var tokenParser = choice(
    keywordTokenParser(OPENING_BRACE_TYPE, "{"),
    keywordTokenParser(CLOSING_BRACE_TYPE, "}"),
    keywordTokenParser(OPENING_BRACKET_TYPE, "["),
    keywordTokenParser(CLOSING_BRACKET_TYPE, "]"),
    keywordTokenParser(COMMA_TYPE, ","),
    keywordTokenParser(COLON_TYPE, ":"),
    keywordTokenParser(TRUE_TYPE, "true"),
    keywordTokenParser(FALSE_TYPE, "false"),
    keywordTokenParser(NULL_TYPE, "null"),
    modify(d -> JsonToken(NUMBER_TYPE, JsonTokenData(d)), decimalParser),
    stringTokenParser,
);

parseDecimal(input): Decimal {
    var it = iterator(input);
    var r = require(decimalParser(it));
    if (hasValue?(nextValue(it))) {
        throw Error(CStringRef());
    }
    return r;
}

var spaceParser = zeroOrMore(condition(asciiSpace?));

var tokensParser = modify(x -> x.1, [spaceParser, separatedList(spaceParser, tokenParser), spaceParser]);

private tokenize(input): Vector[JsonToken] {
    var iterator = iterator(input);
    var result = require(tokensParser(iterator));
    var next = nextValue(iterator);
    if (hasValue?(next)) {
        error("not exhausted");
    }
    return result;
}


// parser

private var jsonCombinators = Combinators[Type(iterator(Vector[JsonToken]()))]();

private tokenTypeParser(tokenType) = jsonCombinators.condition(t => t.type == tokenType);

private var stringValueParser = jsonCombinators.modify(t -> variantAs(t.data, UTF8String), tokenTypeParser(STRING_TYPE));

private var nullParser = jsonCombinators.modify(t -> JsonNull(), tokenTypeParser(NULL_TYPE));
private var trueParser = jsonCombinators.modify(t -> JsonBoolean(true), tokenTypeParser(TRUE_TYPE));
private var falseParser = jsonCombinators.modify(t -> JsonBoolean(false), tokenTypeParser(FALSE_TYPE));
private var numberParser = jsonCombinators.modify(t -> JsonNumber(variantAs(t.data, Decimal)), tokenTypeParser(NUMBER_TYPE));
private var stringParser = jsonCombinators.modify(s -> JsonString(s), stringValueParser);

private var objectParser =
    jsonCombinators.modify(
        x -> JsonObject(x.1),
        [
            tokenTypeParser(OPENING_BRACE_TYPE),
            jsonCombinators.optSeparatedList(
                tokenTypeParser(COMMA_TYPE),
                jsonCombinators.modify(x -> [x.0, x.2], [
                    stringValueParser,
                    tokenTypeParser(COLON_TYPE),
                    valueParser,
                ]),
            ),
            tokenTypeParser(CLOSING_BRACE_TYPE),
        ]
    );

private var arrayParser =
    jsonCombinators.modify(
        x -> JsonArray(x.1),
        [
            tokenTypeParser(OPENING_BRACKET_TYPE),
            jsonCombinators.optSeparatedList(
                tokenTypeParser(COMMA_TYPE),
                valueParser,
            ),
            tokenTypeParser(CLOSING_BRACKET_TYPE),
        ]
    );

[V]
private variantParser(#V, ..parsers) {
    return jsonCombinators.choice(..mapValues(p => jsonCombinators.modify(V, p), ..parsers));
}

private valueParser(input): Maybe[JsonValue] =
    variantParser(JsonValue,
        nullParser, trueParser, falseParser, numberParser, stringParser, arrayParser, objectParser)(input);

parseJson(input): JsonValue {
    var tokens = tokenize(input);
    return require(valueParser(iterator(tokens)));
}

